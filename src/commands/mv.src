import_code("/src/utils.src")
import_code("/src/shell.src")

CommandMv = {}
CommandMv.cmd = "mv"
CommandMv.alias = ""
CommandMv.args = "SOURCE DEST" // TODO: Add -n option
CommandMv.desc = "Move or rename files and directories"
CommandMv.man = "No manual entry for mv"
CommandMv.usage = "Usage: "+CommandMv.cmd+" "+CommandMv.args

CommandMv.exec = function(args)
    if args.len != 2 then
        GfmShell.println(CommandMv.usage)
        return
    end if

    origFileName = args[0]
    destFileName = args[1]

    origFile = resolveFilePath(origFileName, GfmShell.context.currentFolder)
    if origFile == null then origFile = resolveFolderPath(origFileName, GfmShell.context.currentFolder)

    if origFile == null then
        GfmShell.println("mv: cannot stat '"+origFileName+"': No such file or directory")
        return
    end if

    destFile = resolveFilePath(destFileName, GfmShell.context.currentFolder)
    if destFile == null then destFile = resolveFolderPath(destFileName, GfmShell.context.currentFolder)

    if destFile == null then
        destNewName = destFileName.split("/")[-1]
        destFileNameParent = destFileName.split("/")[:-1].join("/")
        destFileParent = resolveFolderPath(destFileNameParent, GfmShell.context.currentFolder)

        if destFileParent == null then
            GfmShell.println("mv: cannot move '"+origFileName+"' to '"+destFileName+"': No such file or directory")
            return
        end if
    else
        if destFile.is_folder then
            destNewName = origFileName.split("/")[-1] // Have this to print error message
            destFileNameParent = destFileName
            destFileParent = destFile
        else
            destNewName = destFileName.split("/")[-1]
            destFileNameParent = destFileName.split("/")[:-1].join("/") // Have this to print error message
            destFileParent = destFile.parent
        end if
    end if

    existingDestFile = resolveFilePath(destNewName, destFileParent)
    if origFile.is_folder and existingDestFile != null then
        GfmShell.println("mv: cannot overwrite non-directory '"+destFileNameParent+"/"+destNewName+"' with directory '"+origFileName+"'")
        return
    end if

    existingDestFolder = resolveFolderPath(destNewName, destFileParent)
    if (not origFile.is_folder) and existingDestFolder != null then
        GfmShell.println("mv: cannot overwrite directory '"+destFileNameParent+"/"+destNewName+"' with non-directory '"+origFileName+"'")
        return
    end if
    
    output = origFile.move(destFileParent.path, destNewName)
    if output and output != 1 then GfmShell.println(output)
end function
