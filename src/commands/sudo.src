import_code("/src/utils.src")
import_code("/src/shell.src")

CommandSudo = {}
CommandSudo.cmd = "sudo"
CommandSudo.alias = ""
CommandSudo.args = "[-u USER] COMMAND [ARGS]"
CommandSudo.desc = "Run command as other user"
CommandSudo.man = "No manual entry for sudo"
CommandSudo.usage = "Usage: "+CommandSudo.cmd+" "+CommandSudo.args

CommandSudo.exec = function(args)
    if args.len < 1 then
        GfmShell.println(CommandSudo.usage)
        return
    end if

    targetUser = "root"
    argIndex = 0

    // Parse -u USER
    if args.len >= 2 and args[0] == "-u" then
        targetUser = args[1]
        argIndex = 2
    end if

    if args.len <= argIndex then
        GfmShell.println(CommandSudo.usage)
        return
    end if

    command = args[argIndex]
    execArgs = []
    if args.len > argIndex+1 then
        execArgs = args[argIndex+1:]
    end if

    // TODO: Make it work when in other shell than the home shell as well
    if GfmShell.context.currentShell != get_shell then
        GfmShell.println("Currently not supporting sudo in other shell except home.")
        return
    end if

    currentUser = GfmShell.context.currentUser
    currentShell = GfmShell.context.currentShell
    currentComputer = GfmShell.context.currentComputer
    currentFolder = GfmShell.context.currentFolder

    newShell = get_shell(targetUser, user_input("Password: ", true))
    if newShell == null then
        GfmShell.println("sudo: incorrect username or password")
        return
    end if

    GfmShell.context.currentUser = targetUser
    GfmShell.context.currentShell = newShell
    GfmShell.context.currentComputer = newShell.host_computer
    GfmShell.context.currentFolder = newShell.host_computer.File(currentFolder.path)

    GfmShell.execCommand(command, execArgs)

    GfmShell.context.currentUser = currentUser
    GfmShell.context.currentShell = currentShell
    GfmShell.context.currentComputer = currentComputer
    GfmShell.context.currentFolder = currentFolder
end function
