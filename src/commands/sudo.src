import_code("/src/utils.src")
import_code("/src/shell.src")

CommandSudo = {}
CommandSudo.cmd = "sudo"
CommandSudo.alias = ""
CommandSudo.args = "[-u USER] EXECUTABLE [ARGS]"
CommandSudo.desc = "Run executable as other user"
CommandSudo.man = "No manual entry for sudo"
CommandSudo.usage = "Usage: "+CommandSudo.cmd+" "+CommandSudo.args

CommandSudo.exec = function(args)
    if args.len < 1 then
        GfmShell.println(CommandSudo.usage)
        return
    end if

    targetUser = "root"
    argIndex = 0

    // Parse -u USER
    if args.len >= 2 and args[0] == "-u" then
        targetUser = args[1]
        argIndex = 2
    end if

    if args.len <= argIndex then
        GfmShell.println(CommandSudo.usage)
        return
    end if

    pathFile = args[argIndex]
    exec_args = ""
    if args.len > argIndex+1 then
        exec_args = args[argIndex+1:].join("")
    end if

    file = resolveFilePath(pathFile, GfmShell.context.currentFolder)

    if file == null then
        GfmShell.println("sudo: file not found: " + pathFile)
        return
    end if

    if not file.is_binary then
        GfmShell.println("sudo: can't execute " + file.path + ". Not binary file")
        return
    end if

    // TODO: Make it work when in other shell than the home shell as well
    if GfmShell.context.currentShell != get_shell then
        GfmShell.println("Currently not supporting sudo in other shell except home.")
        return
    end if

    newShell = get_shell(targetUser, user_input("Password: ", true))
    if newShell == null then
        GfmShell.println("sudo: incorrect username or password")
        return
    end if

    if not file.has_permission("x") then
        GfmShell.println("sudo: permission denied")
        return
    end if

    output = newShell.launch(file.path, exec_args)
    if output and output != 1 then GfmShell.println(output)
end function
