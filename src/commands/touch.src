import_code("/src/utils.src")
import_code("/src/shell.src")

CommandTouch = {}
CommandTouch.cmd = "touch"
CommandTouch.alias = ""
CommandTouch.args = "FILE"
CommandTouch.desc = "Create an empty file"
CommandTouch.man = "No manual entry for touch"
CommandTouch.usage = "Usage: "+CommandTouch.cmd+" "+CommandTouch.args

CommandTouch.exec = function(args)
    //command: touch
    if args.len != 1 then
        GfmShell.println(CommandTouch.usage)
        return
    end if

    pathFile = args[0]

    fileName = pathFile.split("/")[-1]
    pathFolder = pathFile.split("/")[:-1].join("/")

    folder = resolveFolderPath(pathFolder, GfmShell.context.currentFolder)
    if folder == null then
        if pathFile.indexOf("/") != null then
            GfmShell.println("touch: cannot touch '"+pathFile+"': No such file or directory")
            return
        end if

        folder = GfmShell.context.currentFolder
    end if

    if resolveFilePath(pathFile, GfmShell.context.currentFolder) != null or resolveFolderPath(pathFile, GfmShell.context.currentFolder) != null then
        GfmShell.println("touch: cannot create file '"+pathFile+"': File exists")
        return
    end if

    if not folder.has_permission("w") then
        GfmShell.println("touch: permission denied")
        return
    end if

    output = GfmShell.context.currentComputer.touch(folder.path, fileName)
    if output and output != 1 then GfmShell.println(output)
end function
