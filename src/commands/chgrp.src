import_code("/src/utils.src")
import_code("/src/shell.src")

CommandChgrp = {}
CommandChgrp.cmd = "chgrp"
CommandChgrp.alias = ""
CommandChgrp.args = "[-R] GROUP FILE"
CommandChgrp.desc = "Changes file group"
CommandChgrp.man = "No manual entry for chgrp"
CommandChgrp.usage = "Usage: "+CommandChgrp.cmd+" "+CommandChgrp.args

CommandChgrp.exec = function(args)
    if args.len < 1 then
        GfmShell.println(CommandChgrp.usage)
        return
    end if

    recursive = false  // `-R`: Recursive

    args_after_flag = 2
    fileArgIndex = null
    for argIndex in range(0, args.len-1)
        arg = args[argIndex]
        if arg[0] == "-" then
            for flag in arg[1:]  // Start at index 1 to skip the '-'
                if flag == "R" then; recursive = true;
                end if
            end for
        else if argIndex == args.len-args_after_flag then
            fileArgIndex = argIndex
            break
        else
            GfmShell.println(CommandChgrp.usage)
            return
        end if
    end for

    if fileArgIndex == null then
        GfmShell.println(CommandChgrp.usage)
        return
    end if

        
    group = args[fileArgIndex+0]
    pathFile = args[fileArgIndex+1]

    file = resolveFilePath(pathFile, GfmShell.context.currentFolder)
    if file != null then
        output = file.set_group(group, recursive)
        if output.len > 0 then GfmShell.println(output)
        return
    end if

    file = resolveFolderPath(pathFile, GfmShell.context.currentFolder)
    if file != null then
        output = file.set_group(group, recursive)
        if output.len > 0 then GfmShell.println(output)
        return
    end if

    GfmShell.println("chgrp: cannot access '"+pathFile+"': No such file or directory")
end function
