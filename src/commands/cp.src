import_code("/src/utils.src")
import_code("/src/shell.src")

CommandCp = {}
CommandCp.cmd = "cp"
CommandCp.alias = ""
CommandCp.args = "[-r] SOURCE DEST" // TODO: Add -n option
CommandCp.desc = "Copy files or directories"
CommandCp.man = "No manual entry for cp"
CommandCp.usage = "Usage: "+CommandCp.cmd+" "+CommandCp.args

CommandCp.exec = function(args)
    if args.len < 1 then
        GfmShell.println(CommandCp.usage)
        return
    end if

    recursive = false  // `-r`: Copy directory

    args_after_flag = 2
    fileArgIndex = null
    for argIndex in range(0, args.len-1)
        arg = args[argIndex]
        if arg[0] == "-" then
            for flag in arg[1:]  // Start at index 1 to skip the '-'
                if flag == "r" then; recursive = true;
                end if
            end for
        else if argIndex == args.len-args_after_flag then
            fileArgIndex = argIndex
            break
        else
            GfmShell.println(CommandCp.usage)
            return
        end if
    end for

    if fileArgIndex == null then
        GfmShell.println(CommandCp.usage)
        return
    end if

    origFileName = args[fileArgIndex+0]
    destFileName = args[fileArgIndex+1]

    origFile = resolveFilePath(origFileName, GfmShell.context.currentFolder)
    if origFile == null then origFile = resolveFolderPath(origFileName, GfmShell.context.currentFolder)

    if origFile == null then
        GfmShell.println("cp: cannot stat '"+origFileName+"': No such file or directory")
        return
    else if origFile.is_folder and not recursive then
        GfmShell.println("cp: -r not specified; omitting directory '"+origFileName+"'")
        return
    end if

    destFile = resolveFilePath(destFileName, GfmShell.context.currentFolder)
    if destFile == null then destFile = resolveFolderPath(destFileName, GfmShell.context.currentFolder)

    if destFile == null then
        destNewName = destFileName.split("/")[-1]
        destFileNameParent = destFileName.split("/")[:-1].join("/")
        destFileParent = resolveFolderPath(destFileNameParent, GfmShell.context.currentFolder)

        if destFileParent == null then
            GfmShell.println("cp: cannot copy '"+origFileName+"' to '"+destFileName+"': No such file or directory")
            return
        end if
    else
        if destFile.is_folder then
            destNewName = removeFromList(origFileName.split("/"), "")[-1] // Have this to print error message
            destFileNameParent = removeFromList(destFileName.split("/"), "").join("")
            destFileParent = destFile
        else
            destNewName = removeFromList(destFileName.split("/"), "")[-1]
            destFileNameParent = removeFromList(destFileName.split("/"), "")[:-1].join("/") // Have this to print error message
            destFileParent = destFile.parent
        end if
    end if

    existingDestFile = resolveFilePath(destNewName, destFileParent)
    if origFile.is_folder and existingDestFile != null then
        GfmShell.println("cp: cannot overwrite non-directory '"+destFileNameParent+"/"+destNewName+"' with directory '"+origFileName+"'")
        return
    end if

    existingDestFolder = resolveFolderPath(destNewName, destFileParent)
    if (not origFile.is_folder) and existingDestFolder != null then
        GfmShell.println("cp: cannot overwrite directory '"+destFileNameParent+"/"+destNewName+"' with non-directory '"+origFileName+"'")
        return
    end if
    
    output = origFile.copy(destFileParent.path, destNewName)
    if output and output != 1 then GfmShell.println(output)
end function
