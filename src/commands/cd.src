import_code("/src/utils.src")
import_code("/src/shell.src")

CommandCd = {}
CommandCd.cmd = "cd"
CommandCd.alias = ""
CommandCd.args = "[PATH]"
CommandCd.desc = "Move through directories"
CommandCd.man = "No manual entry for cd"
CommandCd.usage = "Usage: "+CommandCd.cmd+" "+CommandCd.args

CommandCd.exec = function(args)
    if args.len > 1 then
        GfmShell.println(CommandCd.usage)
        return
    end if

    targetPath = null
    if args.len == 0 then
        if GfmShell.context.currentUser == "root" then
            targetPath = "/"
        else
            targetPath = "/home/" + GfmShell.context.currentUser
        end if
    else
        targetPath = args[0]
    end if
    if targetPath == null then exit("Error in [cd]: targetPath is null (This is a bug).")

    // Resolve the target path
    if targetPath == ".." then
        // Move up one level
        if GfmShell.context.currentFolder.parent != null then
            GfmShell.context.currentFolder = GfmShell.context.currentFolder.parent
        else
            GfmShell.println("Already at the root directory.")
        end if
        return
    end if


    targetFolder = resolveFolderPath(targetPath, GfmShell.context.currentFolder)

    if targetFolder == null then
        GfmShell.println("cd: "+targetPath+": No such directory")
        return
    end if

    // Change to the target directory
    GfmShell.context.currentFolder = targetFolder
end function
