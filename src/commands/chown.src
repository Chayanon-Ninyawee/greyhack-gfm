import_code("/src/utils.src")
import_code("/src/shell.src")

CommandChown = {}
CommandChown.cmd = "chown"
CommandChown.alias = ""
CommandChown.args = "[-R] OWNER[:GROUP] FILE"
CommandChown.desc = "Changes file owner"
CommandChown.man = "No manual entry for chown"
CommandChown.usage = "Usage: "+CommandChown.cmd+" "+CommandChown.args

CommandChown.exec = function(args)
    if args.len < 1 then
        GfmShell.println(CommandChown.usage)
        return
    end if

    recursive = false  // `-R`: Recursive

    args_after_flag = 2
    fileArgIndex = null
    for argIndex in range(0, args.len-1)
        arg = args[argIndex]
        if arg[0] == "-" then
            for flag in arg[1:]  // Start at index 1 to skip the '-'
                if flag == "R" then; recursive = true;
                end if
            end for
        else if argIndex == args.len-args_after_flag then
            fileArgIndex = argIndex
            break
        else
            GfmShell.println(CommandChown.usage)
            return
        end if
    end for

    if fileArgIndex == null then
        GfmShell.println(CommandChown.usage)
        return
    end if

        
    ownerGroupStr = args[fileArgIndex+0]
    pathFile = args[fileArgIndex+1]

    owner = null
    group = null

    colonIndex = ownerGroupStr.indexOf(":")

    if colonIndex == null then
        // Only owner
        owner = ownerGroupStr
    else
        // Split
        ownerPart = ownerGroupStr[:colonIndex]
        groupPart = ownerGroupStr[colonIndex+1:]

        if ownerPart.len > 0 then
            owner = ownerPart
        end if

        if groupPart.len > 0 then
            group = groupPart
        end if
    end if


    file = resolveFilePath(pathFile, GfmShell.context.currentFolder)
    if file != null then
        if owner != null then
            output = file.set_owner(owner, recursive)
            if output.len > 0 then GfmShell.println(output)
        end if
        if group != null then
            output = file.set_group(group, recursive)
            if output.len > 0 then GfmShell.println(output)
        end if
        return
    end if

    file = resolveFolderPath(pathFile, GfmShell.context.currentFolder)
    if file != null then
        if owner != null then
            output = file.set_owner(owner, recursive)
            if output.len > 0 then GfmShell.println(output)
        end if
        if group != null then
            output = file.set_group(group, recursive)
            if output.len > 0 then GfmShell.println(output)
        end if
        return
    end if

    GfmShell.println("chmod: cannot access '"+pathFile+"': No such file or directory")
end function
