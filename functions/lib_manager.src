LibManager = {}

LibManager.lib_path = user_folder_path+"/lib"
LibManager.aptClient_path = LibManager.lib_path+"/aptclient.so"
LibManager.metaxploit_path = LibManager.lib_path+"/metaxploit.so"
LibManager.crypto_path = LibManager.lib_path+"/crypto.so"

aptClient = include_lib(LibManager.aptClient_path)
if aptClient == null then
    home_computer = get_shell("root", root_password).host_computer
    aptClient_file = home_computer.File("/lib/aptclient.so")
    if aptClient_file == null then exit("[gfm] Error: Can't find aptclient.so in /lib or "+LibManager.lib_path)

    lib_file = home_computer.File(LibManager.lib_path)
    if lib_file == null then home_computer.create_folder(user_folder_path, "lib")

    copy_result = aptClient_file.copy(LibManager.lib_path, "aptclient.so")
    if not copy_result == 1 then exit("[gfm] Error: Can't copy aptclient.so in /lib to "+LibManager.lib_path)

    aptClient = include_lib(LibManager.aptClient_path)
end if
if aptClient == null then exit("[gfm] Error: Can't find aptclient.so in "+LibManager.lib_path+" Even though it should")

metaxploit = null
crypto = null

// Updates the aptClient, metaxploit, and crypto libraries by checking for and installing any available upgrades.
// Adds a repository to aptClient, installs missing libraries, and handles errors during the upgrade process.
//
// This function performs the following tasks:
// - Adds a apt-get repository.
// - Checks and installs the `metaxploit.so` and `crypto.so` libraries if they are not present.
// - Checks for upgrades to `aptclient.so`, `metaxploit.so`, and `crypto.so`, and installs upgrades if available.
// - Handles errors that occur during the upgrade and installation process, printing relevant error messages.
//
// Side Effects:
// - Modifies the `aptClient`, `metaxploit`, and `crypto` instance by installing/upgrading libraries.
// - May create or modify files in the specified library path (`LibManager.lib_path`).
// - May print error messages and exit the program if any issues are encountered during the upgrade process.
//
// @return {void} - This function does not return a value.
update_lib = function()
    aptClient.add_repo(hackShop_ip)
    update_result = aptClient.update()
    if not update_result == "" then exit("[gfm] Error: Can't update apt-get repo!\n--- Error Info ---\n"+update_result+"\n--- End Error Info ---")
    
    home_computer = get_shell("root", root_password).host_computer
    if home_computer.File(LibManager.metaxploit_path) == null then aptClient.install("metaxploit.so", LibManager.lib_path)
    if home_computer.File(LibManager.crypto_path) == null then aptClient.install("crypto.so", LibManager.lib_path)

    aptClient_upgrade_result = aptClient.check_upgrade(LibManager.aptClient_path)
    if typeof(aptClient_upgrade_result) == "string" then; print("[gfm] Error: Can't upgrade aptclient.so\n--- Error Info ---\n"+aptClient_upgrade_result+"\n--- End Error Info ---");
    else if aptClient_upgrade_result == null then; print("[gfm] Error: Can't upgrade aptclient.so. aptClient.check_upgrade(LibManager.aptClient_path) return null");
    else if aptClient_upgrade_result == 1 then; aptClient.install("aptclient.so", LibManager.lib_path); end if
    globals.aptClient = include_lib(LibManager.aptClient_path)

    metaxploit_upgrade_result = aptClient.check_upgrade(LibManager.metaxploit_path)
    if typeof(metaxploit_upgrade_result) == "string" then; print("[gfm] Error: Can't upgrade metaxploit.so\n--- Error Info ---\n"+metaxploit_upgrade_result+"\n--- End Error Info ---");
    else if metaxploit_upgrade_result == null then; print("[gfm] Error: Can't upgrade metaxploit.so. aptClient.check_upgrade(LibManager.metaxploit_path) return null");
    else if metaxploit_upgrade_result == 1 then; aptClient.install("metaxploit.so", LibManager.lib_path); end if
    globals.metaxploit = include_lib(LibManager.metaxploit_path)

    crypto_upgrade_result = aptClient.check_upgrade(LibManager.crypto_path)
    if typeof(crypto_upgrade_result) == "string" then; print("[gfm] Error: Can't upgrade crypto.so\n--- Error Info ---\n"+crypto_upgrade_result+"\n--- End Error Info ---");
    else if crypto_upgrade_result == null then; print("[gfm] Error: Can't upgrade crypto.so. aptClient.check_upgrade(LibManager.crypto_path) return null");
    else if crypto_upgrade_result == 1 then; aptClient.install("crypto.so", LibManager.lib_path); end if
    globals.crypto = include_lib(LibManager.crypto_path)
end function

metaxploit = include_lib(LibManager.metaxploit_path)
if metaxploit == null then update_lib()
if metaxploit == null then exit("[gfm] Error: Can't find metaxploit.so in "+LibManager.lib_path+" Even though it should")
get_custom_object.home_metaxploit = metaxploit

crypto = include_lib(LibManager.crypto_path)
if crypto == null then update_lib()
if crypto == null then exit("[gfm] Error: Can't find crypto.so in "+LibManager.lib_path+" Even though it should")
get_custom_object.home_crypto = crypto